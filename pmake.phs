#!/usr/bin/env phasor
using("stdsys", "stdtype", "stdio", "stdfile", "stdstr");

var project = "Phasor";
var cacheFolder = ".pmake";
var cacheFile = c_fmt("%s/%%s.cache", cacheFolder);

fn printHelp(program: string, configured: bool) {
    if (configured)
        putf("Usage: phasor %s [preset] [options]", program);
    else
        putf("Usage: phasor %s <preset> [options]\n", program);

    if (!configured) { 
        sys_execute("cmake --list-presets"); 
        print("\nPreset is only needed for config step; once configured no suboptions are required.\n");
    }
    print(`
Options:
  preset           The CMake preset to use for configuration (required if not configured)
  -h, --help       Show this help message
  -i, --install    Install the built files to the specified prefix (default: install)
  -b, --build      Build the project to the specified output folder (default: out)
`);
}

fn configure(srcFolder: string, buildFolder: string, preset: string) -> bool {
    if (sys_execute(c_fmt("cmake -S %s -B %s --preset %s", srcFolder, buildFolder, preset)) != 0) {
        return false;
    }
    return true;
}

fn build(buildFolder: string) -> bool {
    if (sys_execute(c_fmt("cmake --build %s", buildFolder)) != 0) {
        return false;
    }
    return true;
}

fn install(buildFolder: string, prefixFolder: string) -> bool {
    if (sys_execute(c_fmt("cmake --install %s --prefix %s", buildFolder, prefixFolder)) != 0) {
        return false;
    }
    return true;
}

fn writeCache(text: string, cacheName: string) -> bool {
    var fileName = c_fmt(cacheFile, cacheName);
    if (fexists(fileName)) {
        if (!frm(fileName)) {
            puts_error("Failed to delete old cache file");
            return false;
        }
    }
    if (!fwrite(fileName, text)) {
        puts_error("Failed to write cache file");
        return false;
    }
    return true;
}

fn readCache(cacheName: string) -> string {
    var fileName = c_fmt(cacheFile, cacheName);
    if (!fexists(fileName)) {
        return "";
    }
    return fread(fileName);
}

fn main() -> int {
    var preset;
    var prefix = "install";
    var doConfigure = false;
    var doBuild = false;
    var doInstall = false;
    var srcFolder = fcd();
    var outFolder = "out";
    var i = 1;

    if (!fexists(cacheFolder)) {
        if (!fmkdir(cacheFolder)) {
            puts_error("Failed to create cache directory");
            return -1;
        }
    }

    if (sys_argc() > 1 && !starts_with(sys_argv(1), "-")) {
        preset = to_string(sys_argv(1));
        i = 2;
        doConfigure = true;
    } else {
        if (fexists(c_fmt(cacheFile, "preset"))) {
            preset = readCache("preset");
            putf("Using cached preset: %s\n", preset);
        } else {
            printHelp(sys_argv(0), false);
            return -1;
        }
    }

    while (i < sys_argc()) {
        var arg = to_string(sys_argv(i));

        if (arg == "-i" || arg == "--install") {
            doInstall = true;

            if (i + 1 < sys_argc() && !starts_with(sys_argv(i + 1), "-")) {
                prefix = to_string(sys_argv(i + 1));
                i = i + 1;
            }

            writeCache(prefix, "install");
        }
        else if (arg == "-b" || arg == "--build") {
            doBuild = true;

            if (i + 1 < sys_argc() && !starts_with(sys_argv(i + 1), "-")) {
                outFolder = to_string(sys_argv(i + 1));
                i = i + 1;
            }

            writeCache(outFolder, "build");
        }
        else if (arg == "-h" || arg == "--help") {
            printHelp(sys_argv(0), true);
            return 0;
        }
        else {
            putf_error("Unknown argument: %s", arg);
            printHelp(sys_argv(0), true);
            return 1;
        }

        i = i + 1;
    }

    writeCache(preset, "preset");

    if (fexists(c_fmt(cacheFile, "install")))
        prefix = readCache("install");

    if (fexists(c_fmt(cacheFile, "build")))
        outFolder = readCache("build");

    if (!doConfigure && !fexists(outFolder)) 
    { 
        puts("Output missing, configuring...");
        doConfigure = true;
    }

    if (doConfigure) {
    	putf("Configuring %s...", project);
        if (configure(srcFolder, outFolder, preset)) {
            puts_error("Configuration failed");
            return 1;
        }
    }

    if (doBuild) {
        putf("Building %s...", project);
        if (build(outFolder)) {
            puts_error("Build failed");
            return 1;
        }
    }

    if (doInstall) {
	putf("Installing %s...", project);
        if (install(outFolder, prefix)) {
            puts_error("Installation failed");
            return 1;
        }
    }

    return 0;
}

shutdown(main());
